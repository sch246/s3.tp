#for_ i in range(0,32)
    #mc execute store result score # tmp run data get storage temp Location_list[<i>].public
    #mc execute if score # tmp matches 0 run data modify storage temp home.if_public_show set value '{"text":"私人","underlined": true,"color":"dark_red","hoverEvent": {"action": "show_text","value": "/trigger home.public set <i>, 点击将允许其他人传送到你家"},"clickEvent": {"action": "run_command","value": "/trigger home.public set <i>"}}'
    #mc execute if score # tmp matches 1 run data modify storage temp home.if_public_show set value '{"text":"开放","underlined": true,"color":"dark_green","hoverEvent": {"action": "show_text","value": "/trigger home.private set <i>, 点击将禁止其他人传送到你家"},"clickEvent": {"action": "run_command","value": "/trigger home.private set <i>"}}'
    #mc execute if data storage temp Location_list[<i>] run tellraw @s [{"text": "","color": "gray"},{"text": "<i>:","color": "white"},{"storage":"temp","nbt":"Location_list[<i>].Name","interpret": true,"underlined": true,"hoverEvent": {"action": "show_text","value": [{"text": "","color": "dark_purple","italic": true},{"text": "/trigger home set <i>\n","color": "white","italic": false},{"storage":"temp","nbt":"Location_list[<i>].Dimension"},": [",{"storage":"temp","nbt":"Location_list[<i>].x"},", ",{"storage":"temp","nbt":"Location_list[<i>].y"},", ",{"storage":"temp","nbt":"Location_list[<i>].z"},"]\n",{"storage":"temp","nbt":"Location_list[<i>].hover","interpret":true}]},"clickEvent": {"action": "run_command","value": "/trigger home set <i>"}},"\n | ",{"text": "重命名","color": "aqua","underlined": true,"hoverEvent": {"action": "show_text","value": "/trigger home.rename set <i>\n根据面前的告示牌设置名字和详情，手拿染料可以设置颜色"},"clickEvent": {"action": "run_command","value": "/trigger home.rename set <i>"}}," | ",{"storage":"temp","nbt":"home.if_public_show","interpret":true}," | ",{"text": "设置传送牌","color": "#bc8c64","underlined": true,"hoverEvent": {"action": "show_text","value": "/trigger home.portal set <i>\n使眼前的告示牌能点击传送,将覆盖内容"},"clickEvent": {"action": "run_command","value": "/trigger home.portal set <i>"}}," | ",{"text": "重置坐标","color": "gold","underlined": true,"hoverEvent": {"action": "show_text","value": {"text": "/trigger home.reset set <i>"}},"clickEvent": {"action": "run_command","value": "/trigger home.reset set <i>"}}," | ",{"text": "删除","color": "red","underlined": true,"hoverEvent": {"action": "show_text","value": {"text": "/trigger home.del set <i>"}},"clickEvent": {"action": "run_command","value": "/trigger home.del set <i>"}}]

# 当已经达到传送点上限的时候,阻止添加新传送点
execute store result score # tmp run data get storage temp Location_list
scoreboard players operation #left tmp = @s home_limit
scoreboard players operation #left tmp -= # tmp
execute if score # tmp < @s home_limit if score # tmp < #32 int run tellraw @s [{"text": "新建传送点","color": "green","underlined": true,"hoverEvent": {"action": "show_text","value": {"text": "/trigger home.new"}},"clickEvent": {"action": "run_command","value": "/trigger home.new"}},": ",{"score":{"name": "#left","objective": "tmp"}}]
tellraw @s {"text": "退出编辑","color": "red","underlined": true,"hoverEvent": {"action": "show_text","value": {"text": "/trigger home.edit_off"}},"clickEvent": {"action": "run_command","value": "/trigger home.edit_off"}}
